int PointInj=0; /*point injection or well boundary injection*/
int SplitLevel=1; /*how many split levels do you want? Choose from [1,2,3,4,5]*/
int WellMesh=5;

real xMin=0;
real xMax=10;

real yMin=0;
real yMax=10;

real Rwell=0.1;
 

int n=80; /*number of elements along a boundary*/

int labelB=1;
int labelR=2;
int labelProd=3;
int labelT=4;
int labelL=5;
int labelInj=6;

real beta=1;

border GammaB(t=0,1){

    x=xMin+(xMax-xMin)*t;

    y=yMin;

    label=labelB;
}

border GammaBB(t=0,1){

    x=xMin+(xMax-xMin)*(exp(beta*(t))-1)/(exp(beta)-1);

    y=yMin;

    label=labelB;

}

border GammaBInj(t=0,1){

    x=(xMin+Rwell)+(xMax-xMin-Rwell)*(exp(beta*(t))-1)/(exp(beta)-1);

    y=yMin;

    label=labelB;

}

border GammaR(t=1,0){

    x=xMax;

    y=(yMax-Rwell)+(Rwell+yMin-yMax)*(exp(beta*(t))-1)/(exp(beta)-1);

    label=labelR;
}

border GammaProd(t=pi/2,0){

    x=-Rwell*cos(t)+xMax;
    y=-Rwell*sin(t)+yMax;
    label =labelProd;
}

border GammaInj(t=pi/2,0){
    x=Rwell*cos(t)+xMin;
    y=Rwell*sin(t)+yMin;
    label =labelInj;
}

border GammaT(t=0,1){

    x=(xMax-Rwell)+(-xMax-xMin+Rwell)*(exp(beta*t)-1)/(exp(beta)-1);

    y=yMax;

    label=labelT;
}

border GammaL(t=1,0){

    x=0;

    y=yMin+(yMax-yMin)*t;

    label=labelL;
}

border GammaLL(t=1,0){

    x=0;

    y=yMin+(yMax-yMin)*(exp(beta*t)-1)/(exp(beta)-1);

    label=labelL;
}

border GammaLInj(t=1,0){

    x=0;

    y=(yMin+Rwell)+(yMax-yMin-Rwell)*(exp(beta*t)-1)/(exp(beta)-1);

    label=labelL;
}

mesh Th;

if (PointInj){

    plot(GammaBB(n)+GammaR(n)+GammaProd(WellMesh)+GammaT(n)+GammaLL(n),dim=2,wait=1);
 
    Th=buildmesh(GammaBB(n)+GammaR(n)+GammaProd(WellMesh)+GammaT(n)+GammaLL(n));

}
else
{
    plot(GammaBInj(n)+GammaR(n)+GammaProd(WellMesh)+GammaT(n)+GammaLInj(n)+GammaInj(WellMesh),dim=2,wait=1);
 
    Th=buildmesh(GammaBInj(n)+GammaR(n)+GammaProd(WellMesh)+GammaT(n)+GammaLInj(n)+GammaInj(WellMesh));
}


func real Spl(real xx, real yy, real r)
{
    real h;
    if ((xx*xx+yy*yy)<=r*r)
        return 1-sqrt(xx^2+yy^2)/r;
    else
        return 0;
}



fespace Wh(Th,P1);

for (int i=SplitLevel; i>1; i--)
{
    Wh MS;

   
    MS=Spl(x,y,i);

    //plot(MS, wait=1);

    mesh Th2=splitmesh(Th,1+i*MS);

    Th=Th2;
}

plot(Th);